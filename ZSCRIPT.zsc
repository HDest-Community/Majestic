version "4.0"

class MajesticHandler : EventHandler
{
	override void CheckReplacement(ReplaceEvent e)
	{
		if (!e.Replacement)
		{
			return;
		}

		switch (e.Replacement.GetClassName())
		{
			case 'ShotgunReplaces':
				if (random[majrand]() <= 12)
				{
					e.Replacement = "MajesticRandom";
				}
				break;
			case 'HDPistol':
				if (random[majrand]() <= 96)
				{
					e.Replacement = "MajesticRandom";
				}
				break;
			case 'ShellBoxRandom':
				if (random[majrand]() <= 10)
				{
					e.Replacement = "HD500SWLightBoxPickup";
				}
				break;
			case 'ClipMagPickup':
				if (random[majrand]() <= 24)
				{
					e.Replacement = "HDMajesticMag";
				}
				break;
		}
	}

	override void WorldThingSpawned(WorldEvent e)
	{
		let MajesticAmmo = HDAmmo(e.Thing);
		if (MajesticAmmo)
		{
			return;
		}

		switch (MajesticAmmo.GetClassName())
		{
			case 'HD500SWLightAmmo': MajesticAmmo.ItemsThatUseThis.Push("HDMajestic"); break;
			case 'HDMajesticMag': MajesticAmmo.ItemsThatUseThis.Push("HDMajestic"); break;
			case 'HDBattery': MajesticAmmo.ItemsThatUseThis.Push("HDMajestic"); break;
		}
	}
}

class HDMajestic : HDHandgun
{
	enum MajesticFlags
	{
		MJF_JustUnload = 1, 
	}

	enum MajesticProperties
	{
		MJProp_Flags,
		MJProp_LoadType, 
		MJProp_Mag, 
		MJProp_Battery, 
		MJProp_Charge, 
		MJProp_SpentRounds
	}

	override void DetachFromOwner()
	{
		A_ResetCharges();
		Super.DetachFromOwner();
	}
	override bool AddSpareWeapon(actor newowner) { return AddSpareWeaponRegular(newowner); }
	override HDWeapon GetSpareWeapon(actor newowner, bool revMJCe, bool doselect) { return GetSpareWeaponRegular(newowner, revMJCe, doselect); }
	override double GunMass() { return 10; }
	override double WeaponBulk()
	{
		double BaseBulk = 60;
		int Mag = WeaponStatus[MJProp_Mag];
		if (Mag >= 0)
		{
			BaseBulk += HDMajesticMag.EncMagLoaded + Mag * ENC_50SW_LOADED;
		}
		return BaseBulk;
	}
	override string, double GetPickupSprite() { return WeaponStatus[MJProp_Mag] >= 0 ? "MJCGZ0" : "MJCGY0", 0.3; }

	override void InitializeWepStats(bool idfa)
	{
		WeaponStatus[MJProp_Mag] = HDMajesticMag.MagCapacity;
		WeaponStatus[MJProp_Battery] = 20;
		WeaponStatus[MJProp_SpentRounds] = 0;
	}

	override void LoadoutConfigure(string input)
	{
		InitializeWepStats();
	}

	override void ForceBasicAmmo()
	{
		owner.A_TakeInventory("HD500SWLightAmmo");
		owner.A_TakeInventory("HDMajesticMag");
		owner.A_GiveInventory("HDMajesticMag");
	}

	override void DropOneAmmo(int amt)
	{
		if (owner)
		{
			double oldAngle = owner.angle;
			amt = clamp(amt, 1, 10);
			if (owner.CheckInventory("HD500SWLightAmmo", 1))
			{
				owner.A_DropInventory("HD500SWLightAmmo", amt * 10);
				owner.angle += 15;
			}
			else
			{
				owner.A_DropInventory("HDMajesticMag", amt);
				owner.angle += 15;
			}
			if (owner.CheckInventory("HDBattery", 1))
			{
				owner.A_DropInventory("HDBattery", 1);
			}
			owner.angle = oldAngle;
		}
	}

	action void A_CheckMajesticHand()
	{
		bool right = !invoker.wronghand;
		right = right && Wads.CheckNumForName("id", 0) != -1 || !right && Wads.CheckNumForName("id", 0) == -1;
		player.GetPSprite(PSP_WEAPON).sprite = GetSpriteIndex(right ? "MJCGA0" : "MJ2GA0");
	}

	override string GetHelpText()
	{
		return WEPHELP_FIRE.." (hold)  Shoot/Charge\n"
		..WEPHELP_ALTFIRE.."  Cancel charging\n"
		..WEPHELP_FIREMODE.."  Quick-Swap (if available)\n"
		..WEPHELP_RELOAD.."  Reload mag\n"
		..WEPHELP_ALTRELOAD.."  Reload battery\n"
		.."("..WEPHELP_USE..")+"..WEPHELP_UNLOAD.."  Unload mag/battery\n"
		..WEPHELP_MAGMANAGER
		..WEPHELP_UNLOADUNLOAD;
	}

	override string PickupMessage()
	{
		return String.Format("You got the 'Majestic' railgun revolver.");
	}

	override void DrawHUDStuff(HDStatusBar sb, HDWeapon hdw, HDPlayerPawn hpl)
	{
		if (sb.hudlevel == 1)
		{
			int nextMag = sb.GetNextLoadMag(HDMagAmmo(hpl.FindInventory('HDMajesticMag')));
			if (nextMag > 0)
			{
				sb.DrawImage("MJMGA0", (-44, -10), sb. DI_SCREEN_CENTER_BOTTOM, scale: (1.25, 1.25));
			}
			else
			{
				sb.DrawImage("MJMGB0", (-44, -10), sb.DI_SCREEN_CENTER_BOTTOM, alpha: nextMag ? 0.6 : 1.0, scale: (1.25, 1.25));
			}
			sb.DrawNum(hpl.CountInv('HDMajesticMag'), -39, -8, sb.DI_SCREEN_CENTER_BOTTOM); 

			sb.DrawBattery(-60, -10, sb.DI_SCREEN_CENTER_BOTTOM, reloadorder: true);
			sb.DrawNum(hpl.CountInv('HDBattery'), -55, -8, sb.DI_SCREEN_CENTER_BOTTOM);
		}
		
		if (hdw.WeaponStatus[MJProp_Mag] >= 0)
		{
			for (int i = 0; i < HDMajesticMag.MagCapacity; ++i)
			{
				double DrawAngle = i * (360.0 / HDMajesticMag.MagCapacity) - 180;
				vector2 DrawPos = (sin(drawangle), cos(DrawAngle)) * 5;
				sb.Fill(hdw.WeaponStatus[MJProp_Mag] > i ? Color(255, 240, 230, 40) : Color(200, 30, 26, 24), DrawPos.x - 24, DrawPos.y - 21, 3, 3, sb.DI_SCREEN_CENTER_BOTTOM | sb.DI_ITEM_CENTER);
			}
		}
		if (hdw.WeaponStatus[MJProp_Battery] >= 0)
		{
			for (int i = 0; i < 20; ++i)
			{
				double DrawAngle = i * (360.0 / 20.0) - 180;
				vector2 DrawPos = (sin(drawangle), cos(DrawAngle)) * 10;
				sb.Fill(hdw.WeaponStatus[MJProp_Battery] > i ? Color(255, 16, 230, 16) : Color(200, 30, 26, 24), DrawPos.x - 23.5, DrawPos.y - 20.5, 2, 2, sb.DI_SCREEN_CENTER_BOTTOM|sb.DI_ITEM_CENTER);
			}

			for (int i = 0; i < hdw.WeaponStatus[MJProp_Charge] / (MaxCharge / Tiers); ++i)
			{
				Color col;
				switch (i)
				{
					case 0: col = Color(255, 0, 255, 0); break;
					case 1: col = Color(255, 255, 255, 0); break;
					case 2: col = Color(255, 255, 0, 0); break;
				}
				sb.Fill(col, -24, -18 - (2 * i), 3, 1, sb.DI_SCREEN_CENTER_BOTTOM | sb.DI_ITEM_CENTER);
			}
		}
		
	}

	override void DrawSightPicture(HDStatusBar sb, HDWeapon hdw, HDPlayerPawn hpl, bool sightbob, vector2 bob, double fov, bool scopeview, actor hpc, string whichdot)
	{
		int cx, cy, cw, ch;
		[cx, cy, cw, ch] = Screen.GetClipRect();
		sb.SetClipRect(-16 + bob.x, -4 + bob.y, 32, 13, sb.DI_SCREEN_CENTER);
		vector2 bob2 = bob * 2;
		bob2.y = clamp(bob2.y, -8, 8);
		sb.DrawImage("MJTCFRNT", bob2, sb.DI_SCREEN_CENTER | sb.DI_ITEM_TOP, alpha: 0.9, scale: (0.8, 0.6));
		sb.SetClipRect(cx, cy, cw, ch);
		sb.DrawImage("MJTCBACK", bob, sb.DI_SCREEN_CENTER | sb.DI_ITEM_TOP, scale: (0.9, 0.7));
	}

	private action void A_ResetCharges()
	{
		for (int i = 0; i < Tiers; ++i)
		{
			invoker.HasPlayedSound[i] = false;
		}
		invoker.WeaponStatus[MJProp_Charge] = 0;
	}

	const MaxCharge = 40;
	const Tiers = 3;
	private bool HasPlayedSound[Tiers];

	Default
	{
		+HDWEAPON.FITSINBACKPACK
		Weapon.SelectionOrder 300;
		Weapon.SlotNumber 2;
		Weapon.SlotPriority 3;
		HDWeapon.BarrelSize 13, 0.35, 0.5;
		Scale 0.2;
		Tag "'Majestic' railgun revolver";
		HDWeapon.Refid "maj";
	}

	States
	{
		RegistMJCprites:
			MJCG Z 0; MJCG Y 0;
		Spawn:
			MJCG Z -1 NoDelay
			{
				frame = (invoker.WeaponStatus[MJProp_Mag] >= 0 ? 25 : 24);
			}
			Stop;
		Ready:
			MJCG A 0
			{
				A_CheckMajesticHand();
				A_ResetCharges();
			}
			#### # 1 A_WeaponReady(WRF_ALL);
			Goto ReadyEnd;
		Select0:
			MJCG A 0
			{
				if (!CheckInventory("NulledWeapon", 1))
				{
					invoker.wronghand = false;
				}
				A_TakeInventory("NulledWeapon");
				A_CheckMajesticHand();
			}
			Goto Select0Small;
		Deselect0:
			MJCG A 0
			{
				A_CheckMajesticHand();
				A_ResetCharges();
			}
			Goto Deselect0Small;
		User3:
			#### A 0 A_MagManager("HDMajesticMag");
			Goto Ready;

		Fire:
			#### # 0 A_JumpIf(invoker.WeaponStatus[MJProp_Mag] > 0 && invoker.WeaponStatus[MJProp_Battery] > 0, 1);
			Goto Nope;
			#### A 1
			{
				A_WeaponReady(WRF_NOFIRE);
				if (invoker.WeaponStatus[MJProp_Charge] < MaxCharge)
				{
					invoker.WeaponStatus[MJProp_Charge]++;

					int index = invoker.WeaponStatus[MJProp_Charge] / (MaxCharge / Tiers);
					if (index > 0 && !invoker.HasPlayedSound[index - 1])
					{
						invoker.HasPlayedSound[index - 1] = true;
						A_StartSound("Majestic/Charge", 10, pitch: 1.0 + 0.1 * (index - 1));
					}
				}
				if (PressingAltFire())
				{
					A_ResetCharges();
					SetWeaponState("Nope");
					return;
				}
			}
			#### A 0 A_ReFire("Fire");
			#### B 1
			{
				HDPlayerPawn(self).gunbraced = false;
				A_MuzzleClimb(-frandom(0.1, 0.2), -frandom(0.1, 0.2));
			}
			#### C 1 Offset(0, 40)
			{
				HDFlashAlpha(128);
				A_Light1();
				A_StartSound("Majestic/Fire", CHAN_WEAPON);

				HDB_500SWElectrified b = HDB_500SWElectrified(HDBulletActor.FireBullet(self, "HDB_500SWElectrified", speedfactor: frandom(0.98, 1.02) + 1.5 * (invoker.WeaponStatus[MJProp_Charge] / double(MaxCharge))));
				b.Charge = invoker.WeaponStatus[MJProp_Charge];
				A_ResetCharges();
				A_AlertMonsters();
				A_ZoomRecoil(0.98);
				A_MuzzleClimb(-frandom(0.25, 1.8), -frandom(4.0, 6.0));

				invoker.WeaponStatus[MJProp_SpentRounds]++;
				invoker.WeaponStatus[MJProp_Mag]--;
				invoker.WeaponStatus[MJProp_Battery]--;
			}
			#### D 1 Offset(0, 50);
			#### B 2 Offset(0, 40);
			#### A 2 Offset(0, 32);
			#### A 0 A_ReFire("Fire");
			Goto Ready;

		Reload:
		AltReload:
			#### # 0
			{
				invoker.WeaponStatus[MJProp_Flags] &= ~MJF_JustUnload;

				int type = invoker.WeaponStatus[MJProp_LoadType] = PressingAltReload() ? 1 : 0;
				int mag = invoker.WeaponStatus[type == 1 ? MJProp_Battery : MJProp_Mag];
				bool noMags = HDMagAmmo.NothingLoaded(self, type == 1 ? 'HDBattery' : 'HDMajesticMag');

				if (noMags || mag >= (type == 1 ? GetDefaultByType('HDBattery').MaxPerUnit : HDMajesticMag.MagCapacity))
				{
					SetWeaponState("Nope");
				}
			}
			Goto RemoveMag;

		Unload:
			#### # 0
			{
				invoker.WeaponStatus[MJProp_Flags] |= MJF_JustUnload;
				int type = invoker.WeaponStatus[MJProp_LoadType] = PressingUse() ? 1 : 0;
				if (invoker.WeaponStatus[type == 1 ? MJProp_Battery : MJProp_Mag] >= 0)
				{
					SetWeaponState("RemoveMag");
				}
			}
			Goto Nope;
		RemoveMag:
			#### # 2 Offset(0, 34) A_SetCrosshair(21);
			#### # 2 Offset(1, 38);
			#### # 3 Offset(2, 42);
			#### # 6 Offset(3, 46) A_StartSound("Majestic/MagOut", 8, CHANF_OVERLAP);
			#### # 0
			{
				int type = invoker.WeaponStatus[MJProp_LoadType];
				int mag = invoker.WeaponStatus[type == 0 ? MJProp_Mag : MJProp_Battery];
				invoker.WeaponStatus[type == 0 ? MJProp_Mag : MJProp_Battery] = -1;
				if (Mag == -1)
				{
					SetWeaponState("MagOut");
					return;
				}

				if (type == 0)
				{
					if (invoker.WeaponStatus[MJProp_SpentRounds] > 0)
					{
						A_StartSound("Majestic/Eject", 9, CHANF_OVERLAP);
					}
					for (int i = 0; i < invoker.WeaponStatus[MJProp_SpentRounds]; ++i)
					{
						A_SpawnItemEx('HDSpent500', 6 * cos(pitch), 0, height / 2 + 6, vel.x, vel.y, vel.z, 0, SXF_ABSOLUTEVELOCITY | SXF_NOCHECKPOSITION | SXF_TRANSFERPITCH);
					}
					invoker.WeaponStatus[MJProp_SpentRounds] = 0;
				}

				class<HDMagAmmo> which = type == 1 ? 'HDBattery' : 'HDMajesticMag';
				if((!PressingUnload() && !PressingReload()) || A_JumpIfInventory(which, 0, "null"))
				{
					HDMagAmmo.SpawnMag(self, which, Mag);
					SetWeaponState("MagOut");
				}
				else
				{
					HDMagAmmo.GiveMag(self, which, Mag);
					A_StartSound("weapons/pocket", 9);
					SetWeaponState("PocketMag");
				}
			}
		PocketMag:
			#### #### 5 Offset(0, 46) A_MuzzleClimb(frandom(-0.2, 0.8), frandom(-0.2, 0.4));
			Goto MagOut;
		MagOut:
			#### # 0
			{
				if (invoker.WeaponStatus[MJProp_Flags] & MJF_JustUnload)
				{
					SetWeaponState("ReloadEnd");
				}
				else
				{
					SetWeaponState("LoadMag");
				}
			}
		LoadMag:
			#### # 4 Offset(0, 46) A_MuzzleClimb(frandom(-0.2, 0.8), frandom(-0.2, 0.4));
			#### # 0 A_StartSound("weapons/pocket", 9);
			#### # 5 Offset(0, 46) A_MuzzleClimb(frandom(-0.2, 0.8), frandom(-0.2, 0.4));
			#### # 3;
			#### # 0
			{
				int type = invoker.WeaponStatus[MJProp_LoadType];
				let mag = HDMagAmmo(FindInventory(type == 1 ? 'HDBattery' : 'HDMajesticMag'));
				if (mag)
				{
					invoker.WeaponStatus[type == 1 ? MJProp_Battery : MJProp_Mag] = mag.TakeMag(true);
					A_StartSound("Majestic/MagIn", 8);
				}
			}
			Goto ReloadEnd;
		ReloadEnd:
			#### # 2 Offset(3, 46);
			#### # 1 Offset(2, 42);
			#### # 1 Offset(2, 38);
			#### # 1 Offset(1, 34);
			Goto Nope;

		// [Ace] I have no idea how this works and I'm too lazy to actually study it.
		// As_long_as_it_just_works.jpg
		Firemode:
		SwapPistols:
			#### A 0 A_SwapHandguns();
			#### A 0 A_JumpIf(player.GetPSprite(PSP_WEAPON).sprite == GetSpriteIndex("MJCGA0"), "SwapPistols2");
		SwapPistols1:
			TNT1 A 0 A_Overlay(1025, "raiseright");
			TNT1 A 0 A_Overlay(1026, "lowerleft");
			TNT1 A 5;
			MJCG A 0;
			Goto Nope;
		SwapPistols2:
			TNT1 A 0 A_Overlay(1025, "raiseleft");
			TNT1 A 0 A_Overlay(1026, "lowerright");
			TNT1 A 5;
			MJ2G A 0;
			Goto Nope;
		LowerLeft:
			MJCG A 0 A_WeaponBusy(true);
			#### A 1 Offset(-6, 38);
			#### A 1 Offset(-12, 48);
			#### A 1 Offset(-20, 60);
			#### A 1 Offset(-34, 76);
			#### A 1 Offset(-50, 86);
			Stop;
		LowerRight:
			MJ2G A 0 A_WeaponBusy(true);
			#### A 1 Offset(6, 38);
			#### A 1 Offset(12, 48);
			#### A 1 Offset(20, 60);
			#### A 1 Offset(34, 76);
			#### A 1 Offset(50, 86);
			Stop;
		RaiseLeft:
			MJCG A 0 A_WeaponBusy(false);
			#### A 1 Offset(-50, 86);
			#### A 1 Offset(-34, 76);
			#### A 1 Offset(-20, 60);
			#### A 1 Offset(-12, 48);
			#### A 1 Offset(-6, 38);
			Stop;
		RaiseRight:
			MJ2G A 0 A_WeaponBusy(false);
			#### A 1 Offset(50, 86);
			#### A 1 Offset(34, 76);
			#### A 1 Offset(20, 60);
			#### A 1 Offset(12, 48);
			#### A 1 Offset(6, 38);
			Stop;
		WhyAreYouSmiling:
			#### A 0 A_WeaponBusy(true);
			#### A 1 Offset(0, 38);
			#### A 1 Offset(0, 38);
			#### A 1 Offset(0, 48);
			#### A 1 Offset(0, 60);
			TNT1 A 7;
			MJ2G A 0
			{
				invoker.wronghand = !invoker.wronghand;
				A_CheckMajesticHand();
			}
			#### A 1 Offset(0, 60);
			#### A 1 Offset(0, 48);
			#### A 1 Offset(0, 38);
			#### A 1 A_WeaponBusy(false);
			Goto Nope;
	}
}

class MajesticExplosion : HDActor
{
	States
	{
		Spawn:
			TNT1 A 0 NoDelay
			{
				target.A_StartSound("weapons/plascrack", 11);
				target.A_StartSound("weapons/plascrack", 12);
				target.A_StartSound("weapons/plascrack", 13);
				target.A_StartSound("world/tbfar", 14);
				target.A_StartSound("world/explode", 15, volume: 0.5);
				
				double fac = 1.0 + (stamina / (HDMajestic.MaxCharge / double(HDMajestic.Tiers)));
				A_HDBlast(
					blastradius: 20 * fac, 
					blastdamage: int(random(20, 80) * fac), 
					blastdamagetype: "slashing");

				actor ltt = Spawn("LingeringThunder", pos, ALLOW_REPLACE);
				ltt.target = target;
				ltt.stamina = stamina;

				A_SprayDecal("MajesticScorch", 14);
				DistantQuaker.Quake(self, int(fac), 35, 512, 8, 128, 256, 256);

				for (int i = 0; i < 1; ++i)
				{
					A_SpawnItemEx("WallChunker", frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), flags: SXF_NOCHECKPOSITION | SXF_TRANSFERPOINTERS);
				}

				DoorDestroyer.DestroyDoor(self, frandom(1, frandom(8, 16) * fac), frandom(1, frandom(4, 16) * fac));

				Spawn("HDExplosion", pos + (frandom(-4, 4), frandom(-4, 4), frandom(-4, 4)), ALLOW_REPLACE);
				for (int i = 0; i < 5 * fac; ++i)
				{
					ArcZap(self);
				}
			}
			Stop;
	}
}

class HDB_500SWElectrified : HDB_500SW
{
	private void Kaboom(vector3 hitpos)
	{
		if (Exploded)
		{
			return;
		}

		Actor exp = Spawn("MajesticExplosion", hitpos, ALLOW_REPLACE);
		exp.stamina = Charge;
		exp.target = self;
		exp.angle = angle;
		exp.pitch = pitch;
		Exploded = true;
	}

	override void OnHitActor(actor hitactor, vector3 hitpos, vector3 vu, int flags)
	{
		Kaboom(hitpos);
		Super.OnHitActor(hitactor, hitpos, vu, flags);
	}

	override void HitGeometry(line hitline, sector hitsector, int hitside, int hitpart, vector3 vu, double lastdist)
	{
		Kaboom(pos - (0, 0, 8));
		Super.HitGeometry(hitline, hitsector, hitside, hitpart, vu, lastdist);
	}

	private bool Exploded;
	int Charge;
}

class MajesticRandom : IdleDummy
{
	States
	{
		Spawn:
			TNT1 A 0 NoDelay
			{
				A_SpawnItemEx("HDMajesticMag", -1, flags: SXF_NOCHECKPOSITION);
				let wpn = HDMajestic(Spawn("HDMajestic", pos, ALLOW_REPLACE));
				if (!wpn)
				{
					return;
				}

				wpn.special = special;
				for (int i = 0; i < 5; ++i)
				{
					wpn.Args[i] = Args[i];
				}
				wpn.InitializeWepStats(false);
			}
			Stop;
	}
}

class HDMajesticMag : HDMagAmmo
{
	override string, string, name, double GetMagSprite(int thismagamt)
	{
		string spr;
		switch (thismagamt)
		{
			case 0: spr = "MJMGC0"; break;
			case 1: spr = "MJMGD0"; break;
			case 2: spr = "MJMGE0"; break;
			case 3: spr = "MJMGF0"; break;
			case 4: spr = "MJMGG0"; break;
			case 5: spr = "MJMGH0"; break;
		}
		return spr, "SWRNA0", "HD500SWLightAmmo", 0.5;
	}

	override void GetItemsThatUseThis()
	{
		ItemsThatUseThis.Push("HDMajestic");
	}

	const MagCapacity = 5;
	const EncMag = 8;
	const EncMagEmpty = EncMag * 0.2;
	const EncMagLoaded = EncMag * 0.2;

	Default
	{
		HDMagAmmo.MaxPerUnit 6;
		HDMagAmmo.InsertTime 9;
		HDMagAmmo.ExtractTime 6;
		HDMagAmmo.RoundType "HD500SWLightAmmo";
		HDMagAmmo.RoundBulk ENC_50SW_LOADED;
		HDMagAmmo.MagBulk EncMagEmpty;
		Tag "'Majestic' magazine";
		Inventory.PickupMessage "Picked up a Majestic magazine.";
		HDPickup.RefId "mjm";
		Scale 0.30;
	}

	States
	{
		Spawn:
			MJMG A -1;
			Stop;
		SpawnEmpty:
			MJMG B -1
			{
				bROLLSPRITE = true;
				bROLLCENTER = true;
				roll = randompick(0, 0, 0, 0, 2, 2, 2, 2, 1, 3) * 90;
			}
			Stop;
	}
}
